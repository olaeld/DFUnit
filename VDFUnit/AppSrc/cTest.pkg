Use cTestCatalog.pkg
Use cObjectNameMixin.pkg
Use cAssertMixin.pkg

Class cTest is a cObject
    Import_Class_Protocol cObjectNameMixin
    Import_Class_Protocol cAssertMixin
    
    Procedure Construct_Object
        Forward Send Construct_Object
        Property String psTestName
        Property Boolean pbFailOccured
        Property Boolean pbWarningOccured
        Property Boolean pbIgnore
        
        //Set this to True if an error SHOULD occur for the test to pass
        Property Boolean pbExpectError
    End_Procedure
    
    Procedure ReportTest Handle hTestFixtureCatalog
        Send AddTestToCatalog to hTestFixtureCatalog Self
    End_Procedure
    
    Function ObjectName Returns String
        String sTestName
        Get psTestName to sTestName
        If (sTestName <> "") Function_Return sTestName
        
        Get LastPartOfObjectName Self to sTestName
        Function_Return sTestName
    End_Function
    
    Procedure OnAssertFailOccured
        Set pbFailOccured to True
    End_Procedure
    
    Procedure OnTestFailed
    End_Procedure
    
    Procedure RunTest
        Set pbFailOccured to False
        Set pbWarningOccured to False
        Move False to Err
        If (pbIgnore(Self)) Set pbWarningOccured to True
        Else Send DoRunTest
    End_Procedure
    
    Procedure DoRunTest
        Send Setup
        Send Test
        If (pbExpectError(Self)) Begin //An error SHOULD occur. If not, Fail!
            If (not(Err)) Begin
                Set pbFailOccured to True
                Error DFERR_PROGRAM "An error was expected, but no error occured!"
            End
            Move False to Err
        End
        Else Begin //Fail if an error occured.
            If (Err) Begin
                Set pbFailOccured to True
            End
        End
        Send TearDown
        If (pbFailOccured(Self)) Send OnTestFailed
    End_Procedure
    
    Procedure Test
    End_Procedure
    
    Procedure Setup
    End_Procedure
    
    Procedure TearDown
    End_Procedure
End_Class
