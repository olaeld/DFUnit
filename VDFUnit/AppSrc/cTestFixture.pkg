Use cTestFixtureCatalog.pkg
Use cTestFixtureRunner.pkg
Use cTestCatalog.pkg
Use cObjectNameMixin.pkg
Use cAssertMixin.pkg
Use cErrorState.pkg

Class cTestFixture is a cObject
    Import_Class_Protocol cObjectNameMixin
    Import_Class_Protocol cAssertMixin
    
    Procedure Construct_Object
        Forward Send Construct_Object
        Property String psTestFixtureName
        Property Handle phTestCatalog
        Property Boolean pbAbortTestFixture False
        
        Object oTestFixtureCatalog is a cTestFixtureCatalog
        End_Object
        Set phTestFixtureNeighborhood of oTestFixtureCatalog to Self
        Object oTestFixtureRunner is a cTestFixtureRunner
        End_Object
        Object oTestCatalog is a cTestCatalog
        End_Object
        Object oErrorState is a cErrorState
        End_Object
        Set phTestCatalog to (oTestCatalog(Self))
    End_Procedure
    
    Function TestFixtureId Returns Handle
        Function_Return Self
    End_Function
    
    Procedure ReportTests
        Handle hTestCatalog
        Get phTestCatalog to hTestCatalog
        If (hTestCatalog = 0) Error DFERR_PROGRAM ("phTestCatalog undefined for" * Name(Self))
        Else Send InitTestCatalog to hTestCatalog
    End_Procedure
    
    Procedure ReportTestFixture Handle hTestFixtureCatalog
        Send AddTestFixtureToCatalog to hTestFixtureCatalog Self
        Send ReportTests
    End_Procedure
    
    Function ObjectName Returns String
        String sTestFixtureName
        Get psTestFixtureName to sTestFixtureName
        If (sTestFixtureName <> "") Function_Return sTestFixtureName
        
        Get LastPartOfObjectName Self to sTestFixtureName
        Function_Return sTestFixtureName
    End_Function
    
    Procedure RunChildTestFixtures
        Set phTestFixtureCatalog of oTestFixtureRunner to oTestFixtureCatalog
        //Todo: List test fixtures in UI
        Send RunTestFixtures to oTestFixtureRunner
    End_Procedure
    
    Procedure CatalogTestFixture
        Send CatalogTestFixtures to oTestFixtureCatalog
    End_Procedure
    
    Procedure ListTestFixture Handle hLister Integer iIndentation
        Send ListTestFixture to hLister (ObjectName(Self)) iIndentation
        Send ListTestFixtures to oTestFixtureCatalog hLister (iIndentation + 1)
    End_Procedure
    
    Procedure RunTestFixture
        Set pbAbortTestFixture to False
        
        Send BeforeSetupOneTime
        If (not(pbAbortTestFixture(Self))) Send SetupOneTime
        
        Send RunChildTestFixtures
        
        If (not(pbAbortTestFixture(Self))) Send RunTests
        Else Error DFERR_PROGRAM "Test Fixture aborted"
        
        If (not(pbAbortTestFixture(Self))) Send TearDownOneTime
        If (not(pbAbortTestFixture(Self))) Send AfterTearDownOneTime
    End_Procedure
    
    Procedure AbortTestFixture
        Set pbAbortTestFixture to True
    End_Procedure
    
    Procedure BeforeSetupOneTime
    End_Procedure
    
    Procedure BeforeSetup
        Set pbAbortTestFixture to False
    End_Procedure
    
    Function ParentFixtureId Returns Handle
        Send SaveErrorState to oErrorState
        Send Ignore_Error to Error_Object_Id DFERR_BAD_MESSAGE
        Handle hTestFixtureIdParent
        Get TestFixtureId of (Parent(Self)) to hTestFixtureIdParent
        Send Trap_Error to Error_Object_Id DFERR_BAD_MESSAGE
        Send RestoreErrorState to oErrorState
        Function_Return hTestFixtureIdParent
    End_Function
    
    Procedure SetupParentFixture
        Handle hTestFixtureIdParent
        Get ParentFixtureId to hTestFixtureIdParent
        If (hTestFixtureIdParent = Parent(Self)) Begin
            Send SetupParentFixture to hTestFixtureIdParent
            Send BeforeSetup to hTestFixtureIdParent
            Send Setup to hTestFixtureIdParent
        End
    End_Procedure
    
    Procedure SetupOneTime
    End_Procedure
    
    Procedure Setup
    End_Procedure
    
    Procedure RunTests
        Handle hTestCatalog hTest
        Get phTestCatalog to hTestCatalog
        Send IteratorReset to hTestCatalog
        While (IteratorMoveNext(hTestCatalog))
            Send BeforeSetup
            Send SetupParentFixture
            Send Setup
            
            Get CurrentTest of hTestCatalog to hTest
            Send RunTest to hTest
            
            Send TearDown
            Send TearDownParentFixture
            Send AfterTearDown
        Loop
    End_Procedure
    
    Procedure TearDownOneTime
    End_Procedure
    
    Procedure AfterTearDownOneTime
    End_Procedure
    
    Procedure TearDown
    End_Procedure
    
    Procedure AfterTearDown
    End_Procedure
    
    Procedure TearDownParentFixture
        Send SaveErrorState to oErrorState
        Send Ignore_Error to Error_Object_Id DFERR_BAD_MESSAGE
        Handle hTestFixtureIdParent
        Get TestFixtureId of (Parent(Self)) to hTestFixtureIdParent
        Send Trap_Error to Error_Object_Id DFERR_BAD_MESSAGE
        Send RestoreErrorState to oErrorState
        If (hTestFixtureIdParent = Parent(Self)) Begin
            Send TearDown to hTestFixtureIdParent
            Send AfterTearDown to hTestFixtureIdParent
            Send TearDownParentFixture to hTestFixtureIdParent
        End
    End_Procedure
    
End_Class
