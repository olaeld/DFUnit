Use cTestCatalog.pkg
Use cObjectNameMixin.pkg
Use cAssertMixin.pkg

Class cTest is a cObject
    Import_Class_Protocol cObjectNameMixin
    Import_Class_Protocol cAssertMixin
    
    Procedure Construct_Object
        Forward Send Construct_Object
        Property String psTestName
        Property Integer piExpectedError
    End_Procedure
    
    Procedure ReportTest Handle hTestFixtureCatalog
        Send AddTestToCatalog to hTestFixtureCatalog Self
    End_Procedure
    
    Function ObjectName Returns String
        String sTestName
        Get psTestName to sTestName
        If (sTestName <> "") Function_Return sTestName
        
        Get LastPartOfObjectName Self to sTestName
        Function_Return sTestName
    End_Function
    
    Procedure ExpectError Integer iErrorNumber
        Set piExpectedError to iErrorNumber
        Send Ignore_Error to Error_Object_Id iErrorNumber
        Move 0 to LastErr
    End_Procedure
    
    Procedure CheckIfExpectedError
        Integer iExpectedError
        Get piExpectedError to iExpectedError
        Boolean bExpectedErrorOccured
        If (iExpectedError <> 0) Begin
            Move (LastErr = iExpectedError) to bExpectedErrorOccured
            Send Trap_Error to Error_Object_Id iExpectedError
            Send Assert bExpectedErrorOccured ("Expected error number" * String(iExpectedError) * "did not occur.")
            If bExpectedErrorOccured Begin
                Move False to Err
                Move 0 to LastErr
            End
            Else Begin
                Move True to Err
                Move DFERR_PROGRAM to LastErr
            End
        End
    End_Procedure
    
    Procedure UnexpectError
        If (piExpectedError(Self) <> 0) Begin
            Set piExpectedError to 0
        End
    End_Procedure
    
    Procedure RunTest
        Send Test
        Send CheckIfExpectedError
        Send UnexpectError
    End_Procedure
    
    Procedure Test
    End_Procedure
    
    Procedure Setup Handle hoTestFixture
        Send Setup to hoTestFixture
    End_Procedure
    
    Procedure TearDown Handle hoTestFixture
        Send TearDown to hoTestFixture
    End_Procedure
End_Class
