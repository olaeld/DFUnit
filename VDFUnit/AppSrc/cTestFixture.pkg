Use cTestFixtureCatalog.pkg
Use cTestCatalog.pkg
Use cObjectNameMixin.pkg
Use cAssertMixin.pkg

Class cTestFixture is a cObject
    Import_Class_Protocol cObjectNameMixin
    Import_Class_Protocol cAssertMixin
    
    Procedure Construct_Object
        Forward Send Construct_Object
        Property String psTestFixtureName
        Property Handle phTestCatalog
        Property Boolean pbAbortTestFixture False
        Property Boolean pbFailOccured
        Property Boolean pbWarningOccured
        Property Boolean pbIgnore
        
        Object oTestCatalog is a cTestCatalog
        End_Object
        Set phTestCatalog to (oTestCatalog(Self))
    End_Procedure
    
    Procedure ReportTests
        Handle hTestCatalog
        Get phTestCatalog to hTestCatalog
        If (hTestCatalog = 0) Error DFERR_PROGRAM ("phTestCatalog undefined for" * Name(Self))
        Else Send InitTestCatalog to hTestCatalog
    End_Procedure
    
    Procedure ReportTestFixture Handle hTestFixtureCatalog
        Send AddTestFixtureToCatalog to hTestFixtureCatalog Self
        Send ReportTests
    End_Procedure
    
    Function ObjectName Returns String
        String sTestFixtureName
        Get psTestFixtureName to sTestFixtureName
        If (sTestFixtureName <> "") Function_Return sTestFixtureName
        
        Get LastPartOfObjectName Self to sTestFixtureName
        Function_Return sTestFixtureName
    End_Function
    
    Procedure RunTestFixture
        If (pbIgnore(Self)) Set pbWarningOccured to True
        Else Send DoRunTestFixture
    End_Procedure
    
    Procedure DoRunTestFixture
        Set pbAbortTestFixture to False
        Set pbFailOccured to False
        Set pbWarningOccured to False
        Send Setup
        If (not(pbAbortTestFixture(Self))) Send RunTests
        If (not(pbAbortTestFixture(Self))) Send TearDown
        Else Error DFERR_PROGRAM "Test Fixture aborted"
    End_Procedure
    
    Procedure AbortTestFixture
        Set pbAbortTestFixture to True
    End_Procedure
    
    Procedure Setup
        Set pbAbortTestFixture to False
    End_Procedure
    
    Procedure RunTests
        Handle hTestCatalog hTest
        Get phTestCatalog to hTestCatalog
        Send IteratorReset to hTestCatalog
        While (IteratorMoveNext(hTestCatalog))
            Get CurrentTest of hTestCatalog to hTest
            Send RunTest to hTest
            If (pbFailOccured(hTest)) Set pbFailOccured to True
            If (pbWarningOccured(hTest)) Set pbWarningOccured to True
        Loop
    End_Procedure
    
    Procedure TearDown
    End_Procedure
    
End_Class
